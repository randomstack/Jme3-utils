plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
}

group = "org.randomstack"
version = "0.2.0"

sourceCompatibility = 1.8

ext {
    jmeVersion = "[3.2,)"
    lombokVersion = "[1.18,)"
    slf4jVersion = "[1.8,)"
    lemurVersion = "[1.12,)"
    commonsTextVersion = "[1.6,)"
    log4j2Version = "[2.11,)"
    jacksonVersion = "[2.9,)"
    junitVersion = "[5.4.0,)"
}

repositories {
    jcenter()
}

configurations {
    compile.transitive = false
}

dependencies {
    // JME
    implementation "org.jmonkeyengine:jme3-core:${jmeVersion}"
    implementation "org.jmonkeyengine:jme3-desktop:${jmeVersion}"
    implementation "org.jmonkeyengine:jme3-lwjgl:${jmeVersion}"

    // GUI
    implementation "com.simsilica:lemur:${lemurVersion}"

    // utilities
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.apache.commons:commons-text:${commonsTextVersion}"

    // logging
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:jul-to-slf4j:${slf4jVersion}"

    // testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

// copy the generated javadocs into the /docs folder for the github pages
task copyJavadoc(dependsOn: javadoc) {
    doLast {
        copy {
            from "$buildDir/docs"
            into "$projectDir/docs"
        }
    }
}

build {
    dependsOn copyJavadoc
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifactId 'jme3-utils'
        }
    }
}
